/*
 * Copyright (c) 2018, The Modern Way. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories { mavenCentral() }
    dependencies {
        classpath(group: 'org.ajoberstar', name: 'grgit', version: '1.7.2')
        classpath(group: 'commons-codec', name: 'commons-codec', version: '1.11')
        classpath(group: 'commons-logging', name: 'commons-logging', version: '1.2')
        classpath(group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5')
        classpath(group: 'org.owasp', name: 'dependency-check-gradle', version: '3.1.1')    
    }
}

import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens
import java.net.InetAddress
import java.util.List

import org.ajoberstar.grgit.Grgit

apply plugin: 'pmd'
apply plugin: 'maven'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'signing'
apply plugin: 'eclipse'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'org.owasp.dependencycheck'

sourceCompatibility = 1.8

targetCompatibility = 1.8

version = '2.0.1-RELEASE'

group = 'com.themodernway'

eclipse {
    classpath { downloadJavadoc = true }
}

checkstyle {
    toolVersion = '7.7'
    ignoreFailures = true
}

tasks.withType(Checkstyle) { source 'src/main' }

task deleteConfigs(type: Delete) {
    FileTree tree = fileTree(dir: "${buildDir}/resources/main")
    tree.each { File file ->
        delete(file)
    }
}

task filterConfigs(dependsOn: deleteConfigs, type: Copy) {
    from sourceSets.main.resources
    into "${buildDir}/resources/main"
    filter(FixCrLfFilter)
    filter(ReplaceTokens, tokens: getFilterTokens())
}

task doJavaDocs(type: Groovydoc) {
    source = sourceSets.main.allSource
    classpath = sourceSets.main.compileClasspath
    destinationDir = file("${buildDir}/docs/javadoc")
    docTitle = "${getProjectCommonName()}"
    footer = "<small><a href=\"${getBuildGITHomeURL()}\" target=\"_blank\">${getProjectCommonName()} - &copy; 2018</a> - <a href=\"${getBuildGITCommitHash()}\" target=\"_blank\">${getBuildModuleVersion()}</a> - <a href=\"mailto:deansjones@gmail.com\">Author: Dean S. Jones</a></small>"
    includes = [
        'com/themodernway/server/integration/**'
    ]
    getGroovydocLinks().each { name, list ->
        link(name, *list)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: doJavaDocs) {
    classifier = 'javadoc'
    from "${buildDir}/docs/javadoc"
}

compileJava.dependsOn(clean)

jar.dependsOn(filterConfigs)

jar {
    manifest {
        attributes 'Implementation-Title': getProjectTitle(), 'Implementation-Version': getBuildModuleVersion()
    }
}

repositories {
    mavenCentral()
    maven { url "${sonatype_snapshotURL}" }
}

task codeCoverage { doLast { println ':codeCoverage-done' } }

codeCoverage.dependsOn jacocoTestReport

jacocoTestReport {
    reports {
        csv {
            enabled true
            destination file("${buildDir}/reports/jacoco/coverage-${version}.csv")
        }
        xml.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

jacocoTestReport.dependsOn test

test {
    minHeapSize = '2048m'
    maxHeapSize = '2048m'
    testLogging.showStandardStreams = true
    jacoco {
        append = false
        destinationFile = file("${buildDir}/jacoco/testing.exec")
    }
}

pmd {
    ignoreFailures = true
    toolVersion = '5.6.1'
    ruleSets = [
        'java-basic',
        'java-braces'
    ]
}

findbugs {
    ignoreFailures = true
    toolVersion = '3.0.1'
    effort = 'max'
    excludeFilter = file('config/findbugs/filter.xml')
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

dependencyCheck {
    failOnError = false
    outputDirectory = "${buildDir}/reports/cve"
    suppressionFile = file('config/cve/suppress.xml')
}

task checkCodeQuality { doLast { println ':checkCodeQuality-done' } }

checkCodeQuality.dependsOn codeCoverage,checkstyleMain,pmdMain,findbugsMain,dependencyCheckAnalyze

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

def getFilterTokens() {
    [GRADLE_BUILD_ARTIFACT_ID: getProjectArtifactID(), GRADLE_BUILD_PROJECT_TITLE: getProjectTitle(), GRADLE_BUILD_USER: getBuildUser(), GRADLE_BUILD_DATE: getBuildDate(), GRADLE_JAVA_VERSION: getJavaVersion(), GRADLE_GIT_COMMIT_HASH: getBuildGITCommitHash(), GRADLE_GIT_COMMIT_USER: getBuildGITCommitUser(), GRADLE_GIT_COMMIT_MESSAGE: getBuildGITCommitMessage(), GRADLE_BUILD_SERVER_HOST: getBuildServerHost(), GRADLE_BUILD_SERVER_BUILD: getBuildServerBuild(), GRADLE_BUILD_MODULE_VERSION: getBuildModuleVersion()]
}

def String getProjectArtifactID() {
    getProjectTitle().toLowerCase()
}

def String getProjectTitle() {
    getProjectCommonName().replaceAll(' ', '-')
}

def String getProjectCommonName() {
    'TheModernWay Server Integration'
}

def String getBuildModuleVersion() {
    version
}

def String getBuildDate() {
    (new Date()).toString()
}

def String getBuildServerHost() {
    try {
        InetAddress.getLocalHost().getHostName()
    }
    catch (Exception e) {
        'localhost'
    }
}

def String getJavaVersion() {
    System.getProperty('java.version') ?: 'unknown'
}

def String getBuildUser() {
    System.getProperty('user.name') ?: 'unknown'
}

def String getBuildServerBuild() {
    System.getenv('BUILD_DISPLAY_NAME') ?: "Command line gradle version ${gradle.gradleVersion}"
}

def String getBuildGITHomeURL() {
    'https://github.com/themodernway/' + getProjectArtifactID()
}

def String getBuildGITCommitHash() {
    getBuildGITHomeURL() + '/commit/' + Grgit.open(project.file('.')).head().id
}

def String getBuildGITCommitUser() {
    Grgit.open(project.file('.')).head().committer.email
}

def String getBuildGITCommitMessage() {
    Grgit.open(project.file('.')).head().shortMessage
}

dependencies {
    compile(group: 'com.themodernway', name: 'themodernway-server-core', version: '2.0.1-RELEASE')
    compile(group: 'org.apache.camel', name: 'camel-core', version: '2.20.2')
    //compile(group: 'org.apache.camel', name: 'camel-cxf', version: '2.20.2')
    compile(group: 'org.apache.camel', name: 'camel-spring', version: '2.20.2')
    compile(group: 'org.apache.camel', name: 'camel-stream', version: '2.20.2')
    compile(group: 'org.apache.camel', name: 'camel-xstream', version: '2.20.2')
    compile(group: 'org.apache.camel', name: 'camel-spring-ws', version: '2.20.2')
    springCorePackages().each { String name ->
        compile(group: 'org.springframework', name: name, version: '5.0.3.RELEASE')
    }
    springSecurityPackages().each { String name ->
        compile(group: 'org.springframework.security', name: name, version: '5.0.1.RELEASE')
    }
    springIntegrationPackages().each { String name ->
        compile(group: 'org.springframework.integration', name: name, version: '5.0.1.RELEASE')
    }
    compile(group: 'org.springframework.integration', name: 'spring-integration-groovy', version: '5.0.1.RELEASE') {
        exclude(group: 'org.codehaus.groovy', module: 'groovy-all')
    }
    compile(group: 'org.springframework.amqp', name: 'spring-rabbit', version: '2.0.2.RELEASE')
    compile(group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1')
    testCompile(group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3')
    testCompile(group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25')
    testCompile(group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.25')
    testCompile(group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.7.25')
    testCompile(group: 'commons-logging', name: 'commons-logging', version: '1.2')
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives jar
}

signing { sign configurations.archives }

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "${sonatype_stageingURL}") {
                //repository(url: "${sonatype_snapshotURL}") {
                authentication(userName: "${sonatype_user_tmw}", password: "${sonatype_password_tmw}")
            }
            pom.project {
                name 'TheModernWay-Server-Integration'
                packaging 'jar'
                description 'TheModernWay Server Integration'
                url 'https://github.com/themodernway/themodernway-server-integration'
                scm {
                    url 'scm:git@github.com:themodernway/themodernway-server-integration.git'
                    connection 'scm:git@github.com:themodernway/themodernway-server-integration.git'
                    developerConnection 'scm:git@github.com:themodernway/themodernway-server-integration.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'sprocketnyc'
                        name 'Dean S. Jones'
                        email 'deansjones@gmail.com'
                        url 'https://www.themodernway.com'
                    }
                }
            }
        }
    }
}

def Map<String, List<String>> getGroovydocLinks() {
    def lmap = [:]
    File data = file('groovydoc/GROOVYDOC_LINK_LISTS.txt')
    if (data.exists()) {
        data.eachLine { line ->
            def vals = ((CharSequence) line).tokenize(',')
            lmap[vals.head()] = vals.tail()
        }
    }
    lmap
}

task buildGroovydocLinks {
    doLast {
        def site = [:]
        File data = file('groovydoc/GROOVYDOC_LINK_SITES.txt')
        if (data.exists()) {
            data.eachLine { line ->
                site[line] = ((CharSequence) new URL("${line}/package-list").text).tokenize('\n')
            }
        }
        File make = file('groovydoc/GROOVYDOC_LINK_LISTS.txt')
        if(make.exists()) {
            make.delete()
        }
        make.createNewFile()
        def save = make.newPrintWriter()
        site.each { link, list ->
            def show = "${link}"
            list.each { line -> show = "${show},${line}" }
            save.println("${show}")
        }
        save.flush()
        save.close()
    }
}

def List<String> springCorePackages() {
    return [
        'spring-messaging',
        'spring-jms'
    ]
}

def List<String> springIntegrationPackages() {
    return [
        'spring-integration-core',
        'spring-integration-ip',
        'spring-integration-ws',
        'spring-integration-jms',
        'spring-integration-jpa',
        'spring-integration-jmx',
        'spring-integration-xml',
        'spring-integration-ftp',
        'spring-integration-sftp',
        'spring-integration-mail',
        'spring-integration-file',
        'spring-integration-http',
        'spring-integration-amqp',
        'spring-integration-event',
        'spring-integration-stream',
        'spring-integration-security',
        'spring-integration-scripting'
    ]
}

def List<String> springSecurityPackages() {
    return [
        'spring-security-messaging'
    ]
}
